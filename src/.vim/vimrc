" Setup vim-plug https://github.com/junegunn/vim-plug/wiki/tips#automatic-installation
let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
if empty(glob(data_dir . '/autoload/plug.vim'))
  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin()
Plug 'mhinz/vim-startify'
Plug 'cocopon/iceberg.vim'
Plug 'vim-airline/vim-airline'
Plug 'easymotion/vim-easymotion'
Plug 'thinca/vim-quickrun'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-fugitive'
Plug 'airblade/vim-gitgutter'
Plug 'voldikss/vim-floaterm'
Plug 'lotabout/skim'
Plug 'lotabout/skim.vim'
Plug 'prabirshrestha/vim-lsp'
Plug 'prabirshrestha/asyncomplete.vim'
Plug 'prabirshrestha/asyncomplete-lsp.vim'
Plug 'hrsh7th/vim-vsnip'
Plug 'hrsh7th/vim-vsnip-integ'
Plug 'dhruvasagar/vim-table-mode'
call plug#end()

let &t_SI = "\e[3 q"
let &t_EI = "\e[1 q"
augroup cursor_shape
  au!
  autocmd CmdlineEnter * call echoraw(&t_SI)
  autocmd CmdlineLeave * call echoraw(&t_EI)
augroup END

set encoding=utf-8
set showcmd
set showmatch
set scrolloff=0
set ignorecase
set smartcase
set hlsearch
set incsearch
set splitright
set splitbelow
set wildmenu
set wildmode=longest,list,full
set backspace=indent,eol,start
set list listchars=tab:\\u00BB\\x20,trail:\\u2601
set expandtab
set tabstop=2
set shiftwidth=0
" Always display
set showtabline=2
set confirm
set mouse=a
syntax on
set background=dark

if has_key(g:plugs, 'vim-startify')
  let g:startify_bookmarks = [
        \ { 'c': '~/.vim/vimrc' },
        \ ]
endif
if has_key(g:plugs, 'iceberg.vim')
  colorscheme iceberg
  " Meta and special keys listed with ':map'
  hi! link SpecialKey Special
endif
if has_key(g:plugs, 'vim-airline')
  let g:airline_section_b = '%{strftime("%c")}'
endif
if has_key(g:plugs, 'vim-easymotion')
  " Turn on case-insensitive feature
  let g:EasyMotion_smartcase = 1
  " keep cursor column when JK motion
  let g:EasyMotion_startofline = 0
  " Disable default mappings
  let g:EasyMotion_do_mapping = 0

  " Jump to anywhere you want with minimal keystrokes, with just one key binding.
  " `s{char}{label}`
  nmap s <Plug>(easymotion-overwin-f)
  " or
  " `s{char}{char}{label}`
  " Need one more keystroke, but on average, it may be more comfortable.
  " nmap s <Plug>(easymotion-overwin-f2)

  " JK motions: Line motions
  map <Leader>j <Plug>(easymotion-j)
  map <Leader>k <Plug>(easymotion-k)

  map <Leader>l <Plug>(easymotion-overwin-line)
endif
if has_key(g:plugs, 'vim-quickrun')
  let g:quickrun_config = {
        \   "make": {
        \     "command": "make",
        \     "exec": "%c %o",
        \     "hook/time/enable": "1",
        \   }
        \ }
endif
if has_key(g:plugs, 'vim-surround')
endif
if has_key(g:plugs, 'vim-commentary')
endif
if has_key(g:plugs, 'vim-fugitive')
endif
if has_key(g:plugs, 'vim-gitgutter')
  augroup gitgutter
    au!
    autocmd BufWritePost * GitGutter
  augroup END
endif
if has_key(g:plugs, 'vim-floaterm')
  nnoremap <silent> <F12> :FloatermToggle<CR>
  tnoremap <silent> <F12> <C-\><C-n>:FloatermToggle<CR>
endif
if has_key(g:plugs, 'skim')
endif
if has_key(g:plugs, 'skim.vim')
endif
if has_key(g:plugs, 'vim-lsp')
  fun! s:on_lsp_buffer_enabled() abort
    let g:lsp_diagnostics_enabled = 1
    let g:lsp_diagnostics_echo_cursor = 1
    let g:lsp_diagnostics_echo_delay = 0
    let g:lsp_diagnostics_float_cursor = 0
    let g:lsp_diagnostics_float_delay = 0
    let g:lsp_diagnostics_signs_delay = 0
    let g:lsp_inlay_hints_enabled = 0
    let g:lsp_diagnostics_virtual_text_enabled = 0
    let g:lsp_diagnostics_virtual_text_insert_mode_enabled = 0
    let g:lsp_format_sync_timeout = 1000
    setlocal omnifunc=lsp#complete
    setlocal signcolumn=yes
    if exists('+tagfunc')
      setlocal tagfunc=lsp#tagfunc
    endif
    nmap <buffer> cd <plug>(lsp-rename)
    nmap <buffer> gd <plug>(lsp-peek-definition)
    nmap <buffer> gD <plug>(lsp-definition)
    nmap <buffer> gh <plug>(lsp-hover)
    nmap <buffer> gi <plug>(lsp-implementation)
    nmap <buffer> gk <plug>(lsp-code-action)
    nmap <buffer> gr <plug>(lsp-references)
    nmap <buffer> gt <plug>(lsp-type-definition)
    nmap <buffer> gs <plug>(lsp-document-symbol-search)
    nmap <buffer> gS <plug>(lsp-workspace-symbol-search)
    nmap <buffer> = <plug>(lsp-document-format)
    nmap <buffer> g[ <plug>(lsp-previous-diagnostic)
    nmap <buffer> g] <plug>(lsp-next-diagnostic)
    nnoremap <buffer> <expr><c-e> len(popup_list()) > 0 ? lsp#scroll(+1) : "\<C-E>"
    nnoremap <buffer> <expr><c-d> len(popup_list()) > 0 ? lsp#scroll(+4) : "\<C-D>"
    nnoremap <buffer> <expr><c-y> len(popup_list()) > 0 ? lsp#scroll(-1) : "\<C-Y>"
    nnoremap <buffer> <expr><c-u> len(popup_list()) > 0 ? lsp#scroll(-4) : "\<C-U>"
    inoremap <buffer> <expr><c-e> len(popup_list()) > 0 ? lsp#scroll(+1) : "\<C-E>"
    inoremap <buffer> <expr><c-d> len(popup_list()) > 0 ? lsp#scroll(+4) : "\<C-D>"
    inoremap <buffer> <expr><c-y> len(popup_list()) > 0 ? lsp#scroll(-1) : "\<C-Y>"
    inoremap <buffer> <expr><c-u> len(popup_list()) > 0 ? lsp#scroll(-4) : "\<C-U>"
  endfun

  if executable('gopls')
    au User lsp_setup call lsp#register_server({
          \   'name': 'gopls',
          \   'cmd': {server_info->['gopls']},
          \   'allowlist': ['go'],
          \ })
  endif
  if executable('pylsp')
    au User lsp_setup call lsp#register_server({
          \   'name': 'pylsp',
          \   'cmd': {server_info->['pylsp']},
          \   'allowlist': ['python'],
          \   'config': {'pylsp': {'plugins': {'autopep8': {'enabled': v:false}}}},
          \ })
  endif
  if executable('rust-analyzer')
    au User lsp_setup call lsp#register_server({
          \   'name': 'rust-analyzer',
          \   'cmd': {server_info->['rust-analyzer']},
          \   'whitelist': ['rust'],
          \   'initialization_options': {
          \     'cargo': {
          \       'buildScripts': {
          \         'enable': v:true,
          \       },
          \     },
          \     'check': {
          \       'command': 'clippy',
          \     },
          \     'completion': {
          \       'snippets': {
          \         'custom': json_decode(join(readfile(expand('~/.vim/snippets/rust.json'))))
          \       }
          \     },
          \   }
          \ })
    augroup rust-analyzer
      au!
      autocmd! BufWritePre <buffer> LspDocumentFormatSync
    augroup END
  endif
  if executable('vim-language-server')
    au User lsp_setup call lsp#register_server({
          \   'name': 'vimls',
          \   'cmd': {server_info->
          \   [&shell, &shellcmdflag, 'vim-language-server --stdio']},
          \   'allowlist': ['vim'],
          \   'semantic_highlight': v:true,
          \ })
  endif
  augroup lsp_install
    au!
    autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
  augroup END
  command! LspDebug
        \ let lsp_log_verbose=1 |
        \ let lsp_log_file = expand('~/lsp.log') |
        \ :vnew | execute "ter ++curwin tail -f " . expand('~/lsp.log') |
        \ :wincmd p
endif
if has_key(g:plugs, 'asyncomplete.vim')
  let g:asyncomplete_popup_delay = 0
  inoremap <expr> <CR> pumvisible() ? asyncomplete#close_popup() : "\<CR>"
endif
if has_key(g:plugs, 'asyncomplete-lsp.vim')
endif
if has_key(g:plugs, 'vim-vsnip')
  let g:vsnip_snippet_dir = '$HOME/.vim/snippets'
  fun! s:handle_imap_tab()
    if vsnip#jumpable(1)
      return "\<Plug>(vsnip-jump-next)"
    endif
    let l:compArg = getline('.')[:col('.')]
    return "\<Tab>"
  endfun
  imap <expr> <Tab> <SID>handle_imap_tab()
  smap <expr> <Tab> vsnip#jumpable(1) ? '<Plug>(vsnip-jump-next)' : '<Tab>'
  smap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : '<S-Tab>'
  imap <expr> <S-Tab> vsnip#jumpable(-1) ? '<Plug>(vsnip-jump-prev)' : '<S-Tab>'
endif
if has_key(g:plugs, 'vim-vsnip-integ')
endif
if has_key(g:plugs, 'vim-table-mode')
endif

augroup rust
  au!
  if has_key(g:plugs, 'vim-quickrun')
    fun! s:initialize_quickrun_config()
      if match(expand("%:p"), "examples") > -1
        let g:quickrun_config = {
              \   "rust/cargo": {
              \     "command": "cargo",
              \     "exec": "%c run --example %s:t:r",
              \     "tempfile": "%{expand('%:p:h')}/%{strftime('%Y%m%d%H%M%S')}.rs",
              \     "hook/time/enable": "1",
              \   }
              \ }
        execute 'nnoremap <silent> <Leader>x :QuickRun rust/cargo<CR>'
      endif
    endfun
    autocmd BufEnter *.rs call s:initialize_quickrun_config()
  endif
augroup END
augroup go
  au!
  autocmd BufReadPre *.go
        \ let g:go_highlight_functions = 1 |
        \ let g:go_highlight_function_calls = 1 |
        \ let g:go_highlight_types = 1 |
        \ let g:go_highlight_operators = 1 |

  fun! s:on_go_loaded() abort
    hi goFunction ctermfg=214
    hi goFunctionCall ctermfg=110
    hi goTypeName ctermfg=140
    hi goTypeConstructor ctermfg=140
    hi goOperator ctermfg=110
    syntax match Bracket /\[\|\]/
    hi Bracket ctermfg=185
  endfun
  autocmd FileType go call s:on_go_loaded()
augroup END
augroup vb
  au!
  au BufReadPost *.bas,*.cls,*.dcm,*.frm edit ++enc=cp932 %
augroup END
augroup git
  au!
  autocmd FileType gitconfig set noexpandtab
augroup END
augroup kdl
  au!
  autocmd FileType kdl set tabstop=4
augroup END

nnoremap <silent> <C-[><C-[> :noh<CR>
nnoremap <C-L> gt
nnoremap <C-H> gT
nnoremap <silent> gb :call NextBuffer(1)<CR>
nnoremap <silent> gB :call NextBuffer(-1)<CR>
nnoremap <silent> g= :set lazyredraw<CR>gg=G`'zz:set nolazyredraw<CR>
nnoremap <silent> gyy "+yy

vnoremap <silent> gy "+y

cnoremap <C-a> <C-b>

function s:prev_word()
  let col = getpos('.')[2]
  let substring = getline('.')[0:col-1]
  let word = matchstr(substring, '\v<(\k(<)@!)*$')
  return word
endfunction

function s:toggle_case(value)
  if matchstr(a:value, '\u') == ""
    return toupper(a:value)
  else
    return tolower(a:value)
  endif
endfunction

inoremap <expr> <C-L> "<C-W>" .. <SID>toggle_case(<SID>prev_word())

fun! PrintSyntaxGroup()
  let l:s = synID(line('.'), col('.'), 1)
  echo synIDattr(l:s, 'name') . ' -> ' . synIDattr(synIDtrans(l:s), 'name')
endfun

fun! NextBuffer(idx)
  let bufs = s:get_normalbuffers()
  if len(bufs) == 0
    echo "No normal buffers available"
    return
  endif
  let dict = s:to_keybased_doubly_circularlist(bufs)
  if !has_key(dict, bufnr())
    echo printf("Current buffer %d is not a normal buffer", bufnr())
    return
  endif
  let n = -1
  if a:idx == 1
    let n = s:to_keybased_doubly_circularlist(bufs)[bufnr()].next.value
  elseif a:idx == -1
    let n = s:to_keybased_doubly_circularlist(bufs)[bufnr()].prev.value
  else
    echo "Argument idx must be 1 or -1"
    return
  endif
  execute "b" n
endfun

fun! s:get_normalbuffers()
  let arr = []
  for buf in getbufinfo({'buflisted': 1})
    " if the buf is a normal buffer and not a netrw buffer
    if getbufvar(buf.bufnr, '&buftype') == ''
          \ && getbufvar(buf.bufnr, 'netrw_browser_active') != 1
      let arr = add(arr,buf.bufnr)
    endif
  endfor
  return arr
endfun

function! s:to_keybased_doubly_circularlist(arr)
  function! s:new_node(value, prev, next)
    let n = {
          \ 'value': a:value,
          \ 'prev': a:prev,
          \ 'next': a:next
          \ }
    return n
  endfunction

  if empty(a:arr)
    return {}
  endif

  let d = {}
  let head = s:new_node(a:arr[0], {}, {})
  let d[head.value] = head

  let cur = head
  for elem in a:arr[1:]
    let node = s:new_node(elem, cur, {})
    let cur.next = node
    let cur = node
    let d[cur.value] = cur
  endfor

  let cur.next = head
  let head.prev = cur
  let d[cur.value] = cur

  return d
endfunction

" Output the circular list dictionary
function! PrintKeyBasedDoublyCircularList(dict)
  for key in keys(a:dict)
    let node = a:dict[key]
    echo printf(
          \ "Value: %s, Prev: %s, Next: %s",
          \ node.value, node.prev.value, node.next.value)
  endfor
endfunction
